#!/usr/bin/env perl

use latest;
use DBI;

use Getopt::Long::Descriptive;

use FindBin qw( $Bin );
use lib "$Bin";
use Path::Tiny;
use utils;

my ( $opt, $usage ) = describe_options(
    "$0 %o",
    [
        'db=s', "sqlite db", { default => path( $Bin )->child( 'metacpan.db' ) }
    ],
    [ 'dist=s',        'dist to blacklist', ],
    [ 'release=s',     'release to blacklist', ],
    [ 'autharchive=s', 'autharchive to blacklist', ],
    [ 'reset',         'reset blacklist status' ],
    [ 'exclude=s@',    'exclude files from blacklist' ],
    [ 'file=s@',   'file to blacklist [+]' ],
    [ 'verbose:+', 'verbose+', ],
);

die( "specify one of --dist or --release\n" )
  if 1 != grep { defined } $opt->dist, $opt->release, $opt->autharchive;

my $dbh = db_connect( $opt->db );

my ( $files, $wanted )  = get_associated_files( $dbh );

if ( $opt->file ) {

    # ensure that the requested files are associated with
    # the given dist/archive
    my %files;
    @files{ @{ $opt->file } } = ( 1 ) x @{ $opt->file };

    my @files = grep { $files{ $_->[0] }
			   and delete $files{ $_->[0] }, 1 } @$files;
    $files = \@files;

    die( "these files aren't part of $wanted:\n",
        join( "\n", sort keys %files ), "\n" )
      if %files;

    if ( $opt->reset ) {
        reset_files( $dbh, $files );
    }

    else {
        blacklist_files( $dbh, $files );
    }

}
else {

    if ( $opt->reset ) {

        reset_autharchive( $dbh, $files->[0][1] )
	    unless $opt->exclude;

        reset_files( $dbh, $files );
    }

    else {
        blacklist_autharchive( $dbh, $files->[0][1] )
	    unless $opt->exclude;

        blacklist_files( $dbh, $files );
    }

}

sub blacklist_autharchive {

    my ( $dbh, $autharchive ) = @_;

    my $nfiles = $dbh->do(
        q[
	  update pass
	     set passed = null
	   where path is null
	   and   autharchive = ?;
	 ],
        {}, $autharchive
    );

    if ( 0 == $nfiles ) {

        my $nfiles = $dbh->do(
            q[
		   insert
		     into pass  (passed, path, autharchive)
		     values     (null, null, ? )
		   ],
            {}, $autharchive
        );

        warn( "error blacklisting autharchive: $autharchive\n" )
          unless $nfiles;
    }
}

sub reset_autharchive {

    my ( $dbh, $autharchive ) = @_;

    my $nfiles = $dbh->do(
        q[
	  delete from pass
	  where path is null
         ],
        {}, $autharchive
    );
}


sub blacklist_files {

    my ( $dbh, $files ) = @_;

    $files = exclude_files( $files );

    my $sth = $dbh->prepare(
        q[
	      update pass
		 set passed = null
	       where path = ?
		 and autharchive = ?;
	     ]
    );

    $files = execute_array( $sth, $files );

    if ( @$files ) {

        my $sth = $dbh->prepare(
            q[
		   insert
		     into pass  (passed, path, autharchive)
		     values     (null, ?, ? )
		   ]
        );

        my $files = execute_array( $sth, $files );

        if ( @$files ) {

            warn(
                "error blacklisting the following:\n",
                map { join( '/', reverse @{$_} ) } @$files
            );
        }
    }

}

sub reset_files {

    my ( $dbh, $files ) = @_;

    $files = exclude_files( $files );

    my $sth = $dbh->prepare(
        q[
	      delete from pass
	       where path = ?
		 and autharchive = ?;
	     ]
    );

    execute_array( $sth, $files );

}

sub get_associated_files {

    my ( $dbh ) = shift;

    my $column;
    my $wanted;

    if ( $opt->dist ) {
        $column = 'distribution';
        $wanted = $opt->dist;
    }

    elsif ( $opt->release ) {
        $column = 'release';
        $wanted = $opt->release;
    }

    elsif ( $opt->autharchive ) {
        $column = 'autharchive';
        $wanted = $opt->autharchive;
    }

    my $files
      = $dbh->selectall_arrayref(
        filter( qw[ path autharchive from ], $column ),
        {}, $wanted );

    die( "no such $column: $wanted\n" )
      unless @$files;

    return ( $files, $wanted );
}

sub exclude_files {

    my $files = shift;

    if ( $opt->exclude ) {

        my %exclude;
        @exclude{ @{ $opt->exclude } } = ( 1 ) x @{ $opt->exclude };

        my @files = grep { !$exclude{ $_->[0] } } @$files;
        $files = \@files;
    }

    return $files;

}

sub execute_array {

    my ( $sth, $files ) = @_;

    my $idx = 0;
    my @status;
    my $tuples = $sth->execute_array( {
            ArrayTupleFetch => sub {
                my $file;
                # skip over undefined slots in $file
                $file = $files->[ $idx++ ]
		    while !defined $file && $idx < @$files;
                return $file;
            },
            ArrayTupleStatus => \@status,
        } );

    if ( defined wantarray() ) {
        # assume errors are due to non-existing files in database
        # delete entries in $files of successful updates
        my @files;
        push @files, $files->[$_]
          foreach grep { ref $status[$_] || $status[$_] == 0 } 0 .. $#{$files};


        return \@files;
    }

    return;
}
